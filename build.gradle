subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    project.group = 'manga-l10n'
    project.version = '1.0-SNAPSHOT'

    compileJava {
        sourceCompatibility = targetCompatibility = 11
        options.encoding = 'UTF-8'
    }

    sourceSets {
        integrationTest {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file('src/integrationTest/java')
            }
            //noinspection GroovyAssignabilityCheck
            resources {
                srcDir file('src/integrationTest/resources')
            }
        }
    }

    configurations {
        integrationTestCompile.extendsFrom testCompile
        integrationTestRuntime.extendsFrom testRuntime
        integrationTestImplementation.extendsFrom testImplementation
    }

    idea {
        module {
            testSourceDirs += sourceSets.integrationTest.java.srcDirs
            testResourceDirs += sourceSets.integrationTest.resources.srcDirs
            scopes.TEST.plus += [ configurations.integrationTestCompile ]
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    ext {
        libraries = [
                lombok    : ['org.projectlombok:lombok:1.18.12'],
                json      : ['org.json:json:20200518'],
                gson      : ['com.google.code.gson:gson:2.8.6'],
                vksdk     : ['com.petersamokhin.vksdk:core-jvm:0.0.5',
                             'com.petersamokhin.vksdk:http-client-jvm-okhttp:0.0.5'],
                yaml      : ['org.yaml:snakeyaml:1.26'],
                commons_io: ['commons-io:commons-io:2.7'],
                okhttp    : ['com.squareup.okhttp3:okhttp:4.8.0'],
                slf4j     : ['org.slf4j:slf4j-api:1.7.30'],
                logback   : ['ch.qos.logback:logback-core:1.2.3',
                             'ch.qos.logback:logback-classic:1.2.3'],
                junit     : ['junit:junit:4.12']
        ]
    }

    dependencies {
        annotationProcessor libraries.lombok
        compileOnly libraries.lombok
        implementation libraries.slf4j

        testImplementation libraries.junit
    }

    task integrationTest(type: Test) {
        group = 'verification'
        testClassesDirs = sourceSets.integrationTest.output.classesDirs
        classpath =  sourceSets.integrationTest.runtimeClasspath
        outputs.upToDateWhen { false }

        options {
            systemProperties(System.getProperties())
        }
    }
}



