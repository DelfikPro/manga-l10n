subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    project.group = 'manga-l10n'
    project.version = '1.0-SNAPSHOT'

    compileJava {
        sourceCompatibility = targetCompatibility = 11
        options.encoding = 'UTF-8'
    }

    sourceSets {
        integrationTest {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file('src/integrationTest/java')
            }
            //noinspection GroovyAssignabilityCheck
            resources {
                srcDir file('src/integrationTest/resources')
            }
        }
    }

    configurations {
        integrationTestCompile.extendsFrom testCompile
        integrationTestRuntime.extendsFrom testRuntime
        integrationTestImplementation.extendsFrom testImplementation
    }

    idea {
        module {
            testSourceDirs += sourceSets.integrationTest.java.srcDirs
            testResourceDirs += sourceSets.integrationTest.resources.srcDirs
            scopes.TEST.plus += [ configurations.integrationTestCompile ]
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()

        //guice-betas
        //https://github.com/mcculls/guice-betas
        //https://github.com/google/guice/issues/1133
        maven { url 'https://mcculls.github.io/guice-betas/maven2/' }
    }

    ext {
        libraries = [
                lombok    : ['org.projectlombok:lombok:1.18.12'],
                gson      : ['com.google.code.gson:gson:2.8.6'],
                yaml      : ['org.yaml:snakeyaml:1.26'],
                commons_io: ['commons-io:commons-io:2.7'],
                okhttp    : ['com.squareup.okhttp3:okhttp:4.8.0'],
                slf4j     : ['org.slf4j:slf4j-api:1.7.30'],
                logback   : ['ch.qos.logback:logback-core:1.2.3',
                             'ch.qos.logback:logback-classic:1.2.3'],
                guice     : ['com.google.inject:guice:4.2.4-20200419-NEWAOP-BETA',
                             'com.google.inject.extensions:guice-multibindings:4.2.3'],
                junit     : ['junit:junit:4.12']
        ]
    }

    dependencies {
        annotationProcessor libraries.lombok
        compileOnly libraries.lombok
        implementation libraries.slf4j
        implementation libraries.guice

        testImplementation libraries.junit
    }

    task integrationTest(type: Test) {
        group = 'verification'
        testClassesDirs = sourceSets.integrationTest.output.classesDirs
        classpath =  sourceSets.integrationTest.runtimeClasspath
        outputs.upToDateWhen { false }

        options {
            systemProperties(System.getProperties())
        }
    }
}



